Query(
  Lambda(
    ["targetTime", "indexName", "size", "after", "before"],
    Let(
      {
        match: Match(Index(Var("indexName"))),
        page: If(
          Equals(Var("before"), null),
          If(
            Equals(Var("after"), null),
            Paginate(Var("match"), { size: Var("size") }),
            Paginate(Var("match"), { after: Var("after"), size: Var("size") })
          ),
          Paginate(Var("match"), { before: Var("before"), size: Var("size") })
        ),
        filtered: Filter(
          Var("page"),
          Lambda(["t", "r"], LT(Var("targetTime"), Var("t")))
        ),
        refs: Map(Var("filtered"), Lambda(["t", "r"], Var("r"))),
        eventList: Map(Var("refs"), Lambda("e", Paginate(Events(Var("e"))))),
        recent: Map(
          Var("eventList"),
          Lambda(
            "el",
            Filter(
              Var("el"),
              Lambda("t", LT(Var("targetTime"), Select("ts", Var("t"))))
            )
          )
        )
      },
      Map(
        Var("recent"),
        Lambda(
          "res",
          Map(
            Var("res"),
            Lambda("event", {
              ts: Select("ts", Var("event")),
              doc: Select("document", Var("event")),
              action: Select("action", Var("event")),
              data: Select("data", Var("event"))
            })
          )
        )
      )
    )
  )
)